'###############################################################################
'#  ComboBoxEdit.bi                                                            #
'#  This file is part of MyFBFramework                                         #
'#  Based on:                                                                  #
'#   TComboBox.bi                                                              #
'#   FreeBasic Windows GUI ToolKit                                             #
'#   Copyright (c) 2007-2008 Nastase Eodor                                     #
'#   Version 1.0.0                                                             #
'#  Updated and added cross-platform                                           #
'#  by Xusinboy Bekchanov (2018-2019)                                          #
'###############################################################################

#include once "ComboBoxEdit.bi"

Namespace My.Sys.Forms
		Private Function ComboBoxEdit.ReadProperty(PropertyName As String) As Any Ptr
			Select Case LCase(PropertyName)
			Case "dropdowncount": Return @FDropDownCount
			Case "integralheight": Return @FIntegralHeight
			Case "itemheight": Return @FItemHeight
			Case "newindex": Return @FNewIndex
			Case "selcolor": Return @FSelColor
			Case "sort": Return @FSort
			Case "style": Return @FStyle
			Case "tabindex": Return @FTabIndex
			Case Else: Return Base.ReadProperty(PropertyName)
			End Select
			Return 0
		End Function
	
		Private Function ComboBoxEdit.WriteProperty(PropertyName As String, Value As Any Ptr) As Boolean
			Select Case LCase(PropertyName)
			Case "designmode": DesignMode = QBoolean(Value): If FDesignMode Then This.AddItem *FName: This.ItemIndex = 0
			Case "dropdowncount": DropDownCount = QInteger(Value)
			Case "integralheight": This.IntegralHeight = QBoolean(Value)
			Case "itemheight": This.ItemHeight = QInteger(Value)
			Case "selcolor": This.SelColor = QInteger(Value)
			Case "sort": This.Sort = QBoolean(Value)
			Case "style": This.Style = *Cast(ComboBoxEditStyle Ptr, Value)
			Case "tabindex": TabIndex = QInteger(Value)
			Case Else: Return Base.WriteProperty(PropertyName, Value)
			End Select
			Return True
		End Function
	
	Private Function ComboBoxEdit.NewIndex As Integer
		Return FNewIndex
	End Function
	
	Private Sub ComboBoxEdit.Undo
			If FEditHandle Then SendMessage(FEditHandle, WM_UNDO, 0, 0)
	End Sub
	
	Private Sub ComboBoxEdit.PasteFromClipboard
			If FEditHandle Then SendMessage(FEditHandle, WM_PASTE, 0, 0)
	End Sub
	
	Private Sub ComboBoxEdit.CopyToClipboard
			If FEditHandle Then SendMessage(FEditHandle, WM_COPY, 0, 0)
	End Sub
	
	Private Sub ComboBoxEdit.CutToClipboard
			If FEditHandle Then SendMessage(FEditHandle, WM_CUT, 0, 0)
	End Sub
	
	Private Sub ComboBoxEdit.SelectAll
			If FEditHandle Then SendMessage(FEditHandle, EM_SETSEL, 0, -1)
	End Sub
	
	Private Property ComboBoxEdit.TabIndex As Integer
		Return FTabIndex
	End Property
	
	Private Property ComboBoxEdit.TabIndex(Value As Integer)
		ChangeTabIndex Value
	End Property
	
	Private Property ComboBoxEdit.TabStop As Boolean
		Return FTabStop
	End Property
	
	Private Property ComboBoxEdit.TabStop(Value As Boolean)
		ChangeTabStop Value
	End Property
	
	Private Sub ComboBoxEdit.ShowDropDown(Value As Boolean)
			Perform CB_SHOWDROPDOWN, Value, 0
	End Sub
	
		Private Function ComboBoxEdit.WindowProc(FWindow As HWND, Msg As UINT, wParam As WPARAM, lParam As LPARAM) As LRESULT
			Select Case Msg
			Case WM_NCCREATE
				'Dim As CreateStruct Ptr CS = Cast(CreateStruct Ptr, lparam)
				'Dim As ComboBoxEdit Ptr CE = New ComboBoxEdit
				'CS->Style = CE->Style
				'CS->dwExStyle = CE->ExStyle
				'lParam = CS
			Case WM_CREATE
				'Dim As CreateStruct Ptr CS = Cast(CreateStruct Ptr, lparam)
				'Dim As ComboBoxEdit Ptr CE = New ComboBoxEdit
				'CS->Style = CE->Style
				'CS->dwExStyle = CE->ExStyle
				'lParam = CS
			End Select
			Return Control.SuperWndProc(FWindow, Msg, wParam, lParam)
		End Function
	
	Private Sub ComboBoxEdit.RegisterClass
			Control.RegisterClass "ComboBoxEdit", "ComboBox", @WindowProc
	End Sub
	
	Private Property ComboBoxEdit.SelColor As Integer
		Return FSelColor
	End Property
	
	Private Property ComboBoxEdit.SelColor(Value As Integer)
		FSelColor = Value
		Invalidate
	End Property
	
	Private Property ComboBoxEdit.Style As ComboBoxEditStyle
		Return FStyle
	End Property
	
	Private Property ComboBoxEdit.Style(Value As ComboBoxEditStyle)
		If Value <> FStyle Then
			FStyle = Value
				Base.Style = WS_CHILD Or WS_VSCROLL Or CBS_HASSTRINGS Or CBS_AUTOHSCROLL Or AStyle(abs_(FStyle)) Or ASortStyle(abs_(FSort)) Or AIntegralHeight(abs_(FIntegralHeight))
		End If
	End Property
	
	Private Property ComboBoxEdit.DropDownCount As Integer
		Return FDropDownCount
	End Property
	
	Private Property ComboBoxEdit.DropDownCount(Value As Integer)
		FDropDownCount = Value
	End Property
	
	Private Property ComboBoxEdit.IntegralHeight As Boolean
		Return FIntegralHeight
	End Property
	
	Private Property ComboBoxEdit.IntegralHeight(Value As Boolean)
		FIntegralHeight = Value
			Base.Style = WS_CHILD Or WS_VSCROLL Or CBS_HASSTRINGS Or CBS_AUTOHSCROLL Or AStyle(abs_(FStyle)) Or ASortStyle(abs_(FSort)) Or AIntegralHeight(abs_(FIntegralHeight))
	End Property
	
	Private Property ComboBoxEdit.ItemCount As Integer
			If Handle Then
				Return Perform(CB_GETCOUNT,0,0)
			End If
		Return Items.Count
	End Property
	
	Private Property ComboBoxEdit.ItemCount(Value As Integer)
	End Property
	
	Private Property ComboBoxEdit.ItemHeight As Integer
			If Handle Then
				If Style <> cbOwnerDrawVariable  Then
					FItemHeight = UnScaleY(Perform(CB_GETITEMHEIGHT, 0, 0))
				End If
			End If
		Return FItemHeight
	End Property
	
	Private Property ComboBoxEdit.ItemHeight(Value As Integer)
		FItemHeight = Value
			If Handle Then
				If Style <> cbOwnerDrawVariable  Then
					Perform(CB_SETITEMHEIGHT, 0, ScaleY(FItemHeight))
				End If
			End If
	End Property
	
	Private Property ComboBoxEdit.ItemIndex As Integer
			If Handle Then FItemIndex = Perform(CB_GETCURSEL, 0, 0)
		Return FItemIndex
	End Property
	
	Private Property ComboBoxEdit.ItemIndex(Value As Integer)
		FItemIndex = Value
			If Handle Then Perform(CB_SETCURSEL, FItemIndex, 0)
			If OnChange Then OnChange(*Designer, This)
	End Property
	
	Private Property ComboBoxEdit.Text ByRef As WString
		If FStyle >= cbDropDownList Then
			If This.ItemIndex > -1 Then
				FText = This.Item(This.ItemIndex)
			Else
				FText = ""
			End If
		Else
				'If This.ItemIndex > -1 Then
				'	FText = This.Item(This.ItemIndex)
				'Else
					Base.Text
				'End If
		End If
		Return WGet(FText.vptr)
	End Property
	
	Private Property ComboBoxEdit.Text(ByRef Value As WString)
		Base.Text = Value
			If FStyle > 1 Then
				If FHandle Then Perform(CB_SELECTSTRING, -1, Cast(LPARAM, FText.vptr))
			Else
				'If FHandle Then SetWindowText(FHandle, FText)
				If FHandle Then Perform(WM_SETTEXT, 0, Cast(LPARAM, FText.vptr))
			End If
			If Items.Count > 0 Then
				Dim As Integer Index = IndexOf(Value)
				If Index >= 0 Then ItemIndex = Index
			End If
	End Property
	
	Private Property ComboBoxEdit.Sort As Boolean
		Return FSort
	End Property
	
	Private Property ComboBoxEdit.Sort(Value As Boolean)
		If Value <> FSort Then
			FSort = Value
				ChangeStyle CBS_SORT, Value
				'Base.Style = WS_CHILD OR WS_VSCROLL OR CBS_HASSTRINGS OR CBS_AUTOHSCROLL OR AStyle(Abs_(FStyle)) OR ASortStyle(Abs_(FSort)) OR AIntegralHeight(Abs_(FIntegralHeight))
		End If
	End Property
	
	Private Property ComboBoxEdit.ItemData(FIndex As Integer) As Any Ptr
		Return Items.Object(FIndex)
	End Property
	
	Private Property ComboBoxEdit.ItemData(FIndex As Integer, Value As Any Ptr)
		Items.Object(FIndex) = Value
	End Property
	
	Private Property ComboBoxEdit.Item(FIndex As Integer) ByRef As WString
		Dim As Integer L
			If Handle Then
				L = Perform(CB_GETLBTEXTLEN, FIndex, 0)
				WReAllocate(FItemText, L)
				Perform(CB_GETLBTEXT, FIndex, Cast(LPARAM, FItemText))
			Else
				WLet(FItemText, Items.Item(FIndex))
			End If
		If FItemText = 0 Then Return WStr("") Else Return *FItemText
	End Property
	
	Private Property ComboBoxEdit.Item(FIndex As Integer, ByRef FItem As WString)
		'Items.Item(FIndex) = FItem  'not refresh
		Dim As Integer CurrentIndex = ItemIndex
		RemoveItem(FIndex)
		InsertItem(FIndex, FItem)
		If CurrentIndex = FIndex Then ItemIndex = CurrentIndex
	End Property
	
	Private Sub ComboBoxEdit.UpdateListHeight
		If Style <> cbSimple Then
				Dim As ..Rect R
				GetWindowRect Handle, @R
				MapWindowPoints 0, GetParent(Handle), Cast(Point Ptr, @R), 2
				MoveWindow Handle, R.Left, R.Top, R.Right - R.Left, R.Bottom - R.Top + ScaleY(ItemHeight * FDropDownCount), 1
		End If
	End Sub
	
	Private Sub ComboBoxEdit.AddItem(ByRef FItem As WString)
		Dim i As Integer
		If FSort Then
			For i = 0 To Items.Count - 1
				If Items.Item(i) > FItem Then Exit For
			Next
			Items.Insert i, FItem
			FNewIndex = i
		Else
			Items.Add(FItem)
			FNewIndex = Items.Count - 1
		End If
			If FHandle Then
				Perform(CB_ADDSTRING, 0, CInt(@FItem))
				UpdateListHeight
			End If
	End Sub
	
	Private Sub ComboBoxEdit.RemoveItem(FIndex As Integer)
		Items.Remove(FIndex)
			If FHandle Then
				Perform(CB_DELETESTRING, FIndex, 0)
				UpdateListHeight
			End If
	End Sub
	
	Private Sub ComboBoxEdit.InsertItem(FIndex As Integer, ByRef FItem As WString)
		If FSort Then
			AddItem FItem
			Exit Sub
		End If
		Items.Insert(FIndex, FItem)
		FNewIndex = FIndex
			If FHandle Then
				Perform(CB_INSERTSTRING, FIndex, CInt(@FItem))
				UpdateListHeight
			End If
	End Sub
	
	Private Function ComboBoxEdit.IndexOf(ByRef FItem As WString) As Integer
		Return Items.IndexOf(FItem) ' Perform(CB_FINDSTRING, -1, CInt(@FItem))
	End Function
	
	Private Function ComboBoxEdit.Contains(ByRef FItem As WString) As Boolean
		Return IndexOf(FItem) <> -1
	End Function
	
	Private Function ComboBoxEdit.IndexOfData(pData As Any Ptr) As Integer
		Return Items.IndexOfObject(pData)
	End Function
	
		Private Function ComboBoxEdit.SubClassProc(FWindow As HWND, Msg As UINT, wParam As WPARAM, lParam As LPARAM) As LRESULT
			Dim As ComboBoxEdit Ptr Ctrl
			Dim As Message Message
			Ctrl = Cast(ComboBoxEdit Ptr, GetWindowLongPtr(FWindow, GWLP_USERDATA))
			Message = Type(Ctrl, FWindow, Msg, wParam, lParam, 0, LoWord(wParam), HiWord(wParam), LoWord(lParam), HiWord(lParam), Message.Captured)
			If Ctrl Then
				With *Ctrl
					If Ctrl->ClassName <> "" Then
						.ProcessMessage(Message)
						If Message.Handled Then
							Return Message.Result
						ElseIf Message.Result = -1 Then
							Return Message.Result
						ElseIf Message.Result = -2 Then
							Msg = Message.Msg
							wParam = Message.wParam
							lParam = Message.lParam
						ElseIf Message.Result <> 0 Then
							Return Message.Result
						End If
					End If
				End With
				Message.Result = CallWindowProc(Ctrl->lpfnEditWndProc, FWindow, Msg, wParam, lParam)
			End If
			Return Message.Result
		End Function
	
		Private Sub ComboBoxEdit.HandleIsAllocated(ByRef Sender As Control)
			If Sender.Child Then
				With QComboBoxEdit(Sender.Child)
						.GetChilds
						If .Style <> cbOwnerDrawVariable AndAlso .FItemHeight <> 0 Then
							.Perform(CB_SETITEMHEIGHT, 0, .ScaleY(.FItemHeight))
						End If
						.UpdateListHeight
						Dim As Integer i
						For i = 0 To .Items.Count - 1
							'						Dim As WString Ptr s = CAllocate_((Len(.Items.Item(i)) + 1) * SizeOf(WString))
							'						*s = .Items.Item(i)
							.Perform(CB_ADDSTRING, 0, CInt(@.Items.Item(i)))
						Next i
						If .FItemIndex <> -1 Then .ItemIndex = .FItemIndex
						.Text = .FText
						If .FEditHandle <> 0 Then
							SetWindowLongPtr(.FEditHandle, GWLP_USERDATA, CInt(.Child))
							.lpfnEditWndProc = Cast(Any Ptr, SetWindowLongPtr(.FEditHandle, GWLP_WNDPROC, CInt(@SubClassProc)))
						End If
				End With
			End If
		End Sub
	
	Private Sub ComboBoxEdit.GetChilds
			Dim As HWND Child
			FEditHandle = 0
			FListHandle = 0
			If Style = cbDropDown Or Style = cbSimple Then
				Child = GetWindow(Handle, GW_CHILD)
				If Child <> 0 Then
					If Style = cbSimple Then
						FListHandle = Child
						Child = GetWindow(Child, GW_HWNDNEXT)
					End If
					FEditHandle = Child
				End If
			End If
	End Sub
	
		Private Sub ComboBoxEdit.SetDark(Value As Boolean)
			Base.SetDark Value
			If Value Then
				SetWindowTheme(FHandle, "DarkMode_CFD", nullptr)
				Brush.Handle = hbrBkgnd
				SendMessageW(FHandle, WM_THEMECHANGED, 0, 0)
				Dim As COMBOBOXINFO cbi
				cbi.cbSize = SizeOf(COMBOBOXINFO)
				Dim As BOOL result = GetComboBoxInfo(FHandle, @cbi)
				If result Then
					If cbi.hwndList Then
						'dark scrollbar for listbox of combobox
						SetWindowTheme(cbi.hwndList, "DarkMode_Explorer", nullptr)
					End If
				End If
			Else
				SetWindowTheme(FHandle, NULL, NULL)
				If FBackColor = -1 Then
					Brush.Handle = 0
				Else
					Brush.Color = FBackColor
				End If
				SendMessageW(FHandle, WM_THEMECHANGED, 0, 0)
				Dim As COMBOBOXINFO cbi
				cbi.cbSize = SizeOf(COMBOBOXINFO)
				Dim As BOOL result = GetComboBoxInfo(FHandle, @cbi)
				If result Then
					If cbi.hwndList Then
						'dark scrollbar for listbox of combobox
						SetWindowTheme(cbi.hwndList, NULL, NULL)
					End If
				End If
			End If
			'SendMessage FHandle, WM_THEMECHANGED, 0, 0
		End Sub
	
	
	Private Sub ComboBoxEdit.ProcessMessage(ByRef Message As Message)
			Select Case Message.Msg
			Case WM_NCCREATE
				
			Case WM_CREATE
				
			Case WM_PAINT
				If g_darkModeSupported AndAlso g_darkModeEnabled Then
					If Not FDarkMode Then
						SetDark True
						'						FDarkMode = True
						'						SetWindowTheme(FHandle, "DarkMode_CFD", nullptr)
						'						Brush.Handle = hbrBkgnd
						'						SendMessageW(FHandle, WM_THEMECHANGED, 0, 0)
						'						Dim As COMBOBOXINFO cbi
						'						cbi.cbSize = SizeOf(COMBOBOXINFO)
						'						Dim As BOOL result = GetComboBoxInfo(FHandle, @cbi)
						'						If result Then
						'							If cbi.hwndList Then
						'								'dark scrollbar for listbox of combobox
						'								SetWindowTheme(cbi.hwndList, "DarkMode_Explorer", nullptr)
						'							End If
						'						End If
					End If
				Else
					If FDarkMode Then
						SetDark False
						'						FDarkMode = False
						'						SetWindowTheme(FHandle, NULL, NULL)
						'						If FBackColor = -1 Then
						'							Brush.Handle = 0
						'						Else
						'							Brush.Color = FBackColor
						'						End If
						'						SendMessageW(FHandle, WM_THEMECHANGED, 0, 0)
						'						Dim As COMBOBOXINFO cbi
						'						cbi.cbSize = SizeOf(COMBOBOXINFO)
						'						Dim As BOOL result = GetComboBoxInfo(FHandle, @cbi)
						'						If result Then
						'							If cbi.hwndList Then
						'								'dark scrollbar for listbox of combobox
						'								SetWindowTheme(cbi.hwndList, NULL, NULL)
						'							End If
						'						End If
					End If
				End If
				Message.Result = 0
			Case CM_CTLCOLOR
				Dim As HDC Dc
				Dc = Cast(HDC, Message.wParam)
				SetBkMode Dc, TRANSPARENT
				SetTextColor Dc, Font.Color
				SetBkColor Dc, This.BackColor
				SetBkMode Dc, OPAQUE
			Case CM_CANCELMODE
				If Message.Sender <> This Then Perform(CB_SHOWDROPDOWN, 0, 0)
			Case CM_COMMAND
				Select Case Message.wParamHi
				Case CBN_SELCHANGE
					If OnChange Then OnChange(*Designer, This)
				Case CBN_SELENDOK
					If OnSelected Then OnSelected(*Designer, This, ItemIndex)
				Case CBN_SELENDCANCEL
					If OnSelectCanceled Then OnSelectCanceled(*Designer, This)
				Case CBN_EDITCHANGE
					If OnChange Then OnChange(*Designer, This)
				Case CBN_EDITUPDATE
				Case CBN_CLOSEUP
					If OnCloseUp Then OnCloseUp(*Designer, This)
				Case CBN_DROPDOWN
					If IntegralHeight = False Then
						If Items.Count Then
							SetWindowPos(Handle, 0, 0, 0, ScaleX(FWidth), ScaleY(ItemHeight * DropDownCount + Height + 2), SWP_NOMOVE Or SWP_NOZORDER Or SWP_NOACTIVATE Or SWP_NOREDRAW Or SWP_HIDEWINDOW)
						Else
							SetWindowPos(Handle, 0, 0, 0, ScaleX(FWidth), ScaleY(ItemHeight + Height + 2) , SWP_NOMOVE Or SWP_NOZORDER Or SWP_NOACTIVATE Or SWP_NOREDRAW Or SWP_HIDEWINDOW)
						End If
						SetWindowPos(Handle, 0, 0, 0, 0, 0, SWP_NOMOVE Or SWP_NOSIZE Or SWP_NOZORDER Or SWP_NOACTIVATE Or SWP_NOREDRAW Or SWP_SHOWWINDOW)
					End If
					If OnDropDown Then OnDropDown(*Designer, This)
				Case CBN_DBLCLK
					If OnDblClick Then OnDblClick(*Designer, This)
				End Select
			Case CM_MEASUREITEM
				Dim As MEASUREITEMSTRUCT Ptr miStruct
				Dim As Integer ItemID
				miStruct = Cast(MEASUREITEMSTRUCT Ptr,Message.lParam)
				ItemID = Cast(Integer,miStruct->itemID)
				If OnMeasureItem Then
					OnMeasureItem(*Designer, This, ItemID, miStruct->itemHeight)
				Else
					miStruct->itemHeight = ItemHeight
				End If
			Case CM_DRAWITEM
				Dim As DRAWITEMSTRUCT Ptr diStruct
				Dim As Integer ItemID,State
				Dim As ..Rect R
				Dim As HDC Dc
				diStruct = Cast(DRAWITEMSTRUCT Ptr,Message.lParam)
				ItemID = Cast(Integer, diStruct->itemID)
				State = Cast(Integer, diStruct->itemState)
				R = Cast(..Rect, diStruct->rcItem)
				Dc = diStruct->hDC
				If (diStruct->itemState And ODS_COMBOBOXEDIT) <> 0 Then State = State Or ODS_COMBOBOXEDIT
				If (diStruct->itemState And ODS_DEFAULT) <> 0 Then State = State Or ODS_DEFAULT
				If OnDrawItem Then
					OnDrawItem(*Designer, This, ItemID, State, *Cast(Rect Ptr, @R), Dc)
				Else
					If (State And ODS_SELECTED) = ODS_SELECTED Then
						Static As HBRUSH B
						If B Then DeleteObject B
						B = CreateSolidBrush(FSelColor)
						FillRect Dc, @R, B
						SetTextColor Dc, clHighlightText
						SetBkColor Dc, FSelColor
						TextOut(Dc,R.Left + 2, R.Top, Item(ItemID),Len(Item(ItemID)))
						If (State And ODS_FOCUS) = ODS_FOCUS Then DrawFocusRect(Dc, @R)
					Else
						FillRect Dc, @R, Brush.Handle
						SetTextColor Dc, Font.Color
						SetBkColor Dc, This.BackColor
						TextOut(Dc, R.Left + 2, R.Top, Item(ItemID), Len(Item(ItemID)))
					End If
				End If
			Case WM_CHAR
				If OnKeyPress Then OnKeyPress(*Designer, This, LoByte(Message.wParam), Message.wParam And &HFFFF)
			Case WM_KEYDOWN
				If OnKeyDown Then OnKeyDown(*Designer, This, Message.wParam, Message.wParam And &HFFFF)
			Case WM_KEYUP
				If Message.wParam = VK_RETURN Then
					If OnActivate Then OnActivate(*Designer, This)
				End If
				If OnKeyUp Then OnKeyUp(*Designer, This, Message.wParam, Message.wParam And &HFFFF)
			End Select
		Base.ProcessMessage(Message)
	End Sub
	
	Private Sub ComboBoxEdit.Clear
		ItemCount = 0
		Items.Clear
			Perform(CB_RESETCONTENT, 0, 0)
	End Sub
	
	Private Sub ComboBoxEdit.SaveToFile(ByRef File As WString)
		Dim As Integer F, i
		Dim As WString Ptr s
		F = FreeFile_
		Open File For Output Encoding "utf-8" As #F
		For i = 0 To ItemCount -1
				Dim TextLen As Integer = Perform(CB_GETLBTEXTLEN, i, 0)
				s = _CAllocate((TextLen + 1) * SizeOf(WString))
				*s = WSpace(TextLen)
				Perform(CB_GETLBTEXT, i, CInt(s))
				Print #F, *s
		Next i
		CloseFile_(F)
		_Deallocate(s)
	End Sub
	
	Private Sub ComboBoxEdit.LoadFromFile(ByRef FileName As WString)
		Dim As Integer F, i
		Dim As WString * 1024 s
		F = FreeFile_
		This.Clear
		Open FileName For Input Encoding "utf-8" As #F
		While Not EOF(F)
			Line Input #F, s
			This.AddItem s
		Wend
		CloseFile_(F)
	End Sub
	
	Private Operator ComboBoxEdit.Cast As Control Ptr
		Return Cast(Control Ptr, @This)
	End Operator
	
	
	Private Constructor ComboBoxEdit
			ASortStyle(abs_(True))   = CBS_SORT
			AStyle(0)          = CBS_SIMPLE
			AStyle(1)          = CBS_DROPDOWN
			AStyle(2)          = CBS_DROPDOWNLIST
			AStyle(3)          = CBS_DROPDOWNLIST Or CBS_OWNERDRAWFIXED
			AStyle(4)          = CBS_DROPDOWNLIST Or CBS_OWNERDRAWVARIABLE
			AIntegralHeight(0) = CBS_NOINTEGRALHEIGHT
			AIntegralHeight(1) = 0
		FStyle              = cbDropDownList
		'ItemHeight          = 13
		FDropDownCount      = 8
		FSelColor           = &H800000
		FIntegralHeight     = 0
		FItemIndex          = -1
		FTabIndex           = -1
		FTabStop            = True
		'Items.Parent        = @This
		With This
			.Child          = @This
			'.ChildProc     = @WindowProc
			'ComboBoxEdit.RegisterClass
			WLet(FClassName, "ComboBoxEdit")
			WLet(FClassAncestor, "ComboBox")
				Base.RegisterClass "ComboBoxEdit", "ComboBox"
				.ExStyle       = 0
				Base.Style     = WS_CHILD Or WS_VSCROLL Or WS_TABSTOP Or CBS_HASSTRINGS Or CBS_AUTOHSCROLL Or AStyle(abs_(FStyle)) Or ASortStyle(abs_(FSort)) Or AIntegralHeight(abs_(FIntegralHeight))
				.BackColor         = GetSysColor(COLOR_WINDOW)
				FDefaultBackColor = .BackColor
				.OnHandleIsAllocated = @HandleIsAllocated
			.Width          = 121
				.Height        = 17
		End With
	End Constructor
	
	Private Destructor ComboBoxEdit
		WDeAllocate(FItemText)
			UnregisterClass "ComboBoxEdit", GetModuleHandle(NULL)
	End Destructor
End Namespace